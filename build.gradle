allprojects {
	group = 'org.rocket'
    version = '1.2-SNAPSHOT'
}

subprojects {
    apply plugin: 'java'
    apply from: rootProject.file('gradle/publish.gradle')

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    ext {
        fungsiVersion = '1.2.+'
        slf4jVersion = '1.7.+'
        guavaVersion = '18.0'
        guiceVersion = '3.0'
        configVersion = '1.2.+'
        acaraVersion = '2.0-alpha3'
        nettyVersion = '4.0.+'
    }

    dependencies {
        testCompile 'junit:junit:4.11'
        testCompile 'org.mockito:mockito-core:1.9.+'
    }
}

apply from: rootProject.file('gradle/jacoco.gradle')

task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}

task docs(type: Javadoc) {
    source subprojects.collect { it.sourceSets.main.allJava }
    classpath = files(subprojects.collect { it.sourceSets.main.compileClasspath })
    destinationDir = file("${buildDir}/docs/javadoc")
    options {
        links "http://docs.oracle.com/javase/8/docs/api/"
        links "http://docs.guava-libraries.googlecode.com/git-history/v18.0/javadoc/"
        links "http://docs.oracle.com/javaee/7/api/"
        links "http://google.github.io/guice/api-docs/3.0/javadoc/"
        links "http://typesafehub.github.io/config/latest/api/"
        links "http://netty.io/4.0/api/"
        links "http://blackrush.github.io/fungsi/1.2/"
        links "http://blackrush.github.io/acara/2.0/"
    }
}
